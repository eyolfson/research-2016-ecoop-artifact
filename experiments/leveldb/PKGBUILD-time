# $Id: PKGBUILD 223591 2014-09-27 18:06:10Z dreisner $
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: feydaykyn <feydaykyn@yahoo.fr>

pkgname=leveldb
pkgver=1.18
pkgrel=1
pkgdesc="A fast and lightweight key/value database library"
arch=('i686' 'x86_64')
url="https://code.google.com/p/leveldb/"
license=('BSD')
depends=('gperftools' 'snappy')
makedepends=('git')
source=("https://github.com/google/leveldb/archive/v$pkgver.tar.gz"
        'remove-opt.patch')
sha1sums=('18684a0ad7a07920d10f5295b171fbf5eeec7337'
          'SKIP')

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i ../remove-opt.patch
}

build() {
  /usr/bin/time -p make -C "$pkgname-$pkgver" &> /tmp/time-leveldb-build
}

check() {
  /usr/bin/time -p make -C "$pkgname-$pkgver" check &> /tmp/time-leveldb-check
}

package() {
  cd "$pkgname-$pkgver"

  install -dm755 \
      "$pkgdir"/usr/{include/leveldb,lib} \
      "$pkgdir"/usr/share/doc/"$pkgname"

  # libraries
  install -m644 -t "$pkgdir/usr/lib" "libleveldb.so.${pkgver%.?*}" libleveldb.a
  cp -P libleveldb.so* "$pkgdir/usr/lib"

  # headers
  install -m644 -t "$pkgdir/usr/include/leveldb" include/leveldb/*

  # documentation
  cp -r doc/* "$pkgdir/usr/share/doc/$pkgname"

  # license
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
